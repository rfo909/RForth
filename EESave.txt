

# Using &CompileBuf 

&CompileBuf CONSTANT Buf (64 bytes)
32 CONSTANT PageSize
0x50 CONSTANT i2cAddr

: Reset (--) Buf BufReset ;
: Add (byte --) Buf BufAdd ;
: Hi (word -- byte) 8 >> 0xFF & ;
: Lo (word -- byte) 0xFF & ;
: AddWord (word -- ) dup Hi Add Lo Add ;


(
Populate page with EEPROM page index as 2 bytes
and up to PageSize bytes of data from the heap
-----------------------------------------------
)
: PopulatePage (pageIndex --)
  => pIndex
  
  Reset
  pIndex 1+ AddWord (write address)
  
  (iterate page pos 0-31)
  0 => ppos
  0 => mempos  (read pos used inside loop)
  
  BEGIN
    &PROTECT 
      pIndex PageSize * + 
      ppos + => mempos
      
    mempos HERE < IF
       mempos readb Add
    THEN
    ppos 1+ => ppos
    ppos PageSize < AGAIN?
;      

(
Save page prepared by PopulatePage to EEPROM
--------------------------------------------
)
: SavePage (--)
  (buf contains two address bytes + data)
  Buf 1+ @ => addr
  "SavePage_ .str addr print# cr
  Buf i2cAddr NATIVE I2C.masterWrite
  i2cAddr NATIVE I2C.masterWWait
;



(
The page at EEPROM address 0x00 contains
meta-information, currently the length only
-------------------------------------------
)
: StatusPage (--)
  HERE &PROTECT - => length
  
  Reset
  (eeprom address 0)
  0 AddWord
 
  (length value)
  length AddWord
  
  SavePage
;


(
Save heap between &PROTECT and HERE. This includes the
memory location where HERE is defined, and other
variable data from ACode. It does not include the stacks,
except the compile stack, because those other stacks
are maintained in C, outside the heap
---------------------------------------------------------
)  
: SaveHeap (--)
  StatusPage
  0 => page
  0 => done
  BEGIN
    page PopulatePage
    Buf readb 2 > IF (more than just pos word)
      SavePage
      page 1+ => page
    ELSE
      1 => done
    THEN
    done not AGAIN?
    cr "Complete .str
;


#END

33 CONSTANT buf#

HERE buf# allot CONSTANT s (send buffer)
HERE buf# allot CONSTANT r (recv buffer)

0x50 CONSTANT &i2c
32 CONSTANT eeloc

: s0 0 s writeb ;

: r<n (n) r writeb ; 
  (n=bytes to read)

: s# s readb ; 
  (length of s)

: s<b (byte) 
  s s# + 1+ writeb 
  s# 1+ s writeb ;
: s<w (word) => w 
  w 8 >> 0xFF & s<b 
  w 0xFF & s<b ;
    
(set data)
: loc eeloc s<w ;
: data 0xCAFE s<w 0xBADE s<w 0xCADA s<w ;

(write to EEPROM)
: write 
  s0 loc data
  s &i2c NATIVE I2C.mW
  (wait a bit)
  &i2c NATIVE I2C.mWWait
;

(read from EEPROM - with mWR)
: read
  s0 loc 
  6 r<n (bytes to read)
  s r &i2c 
    NATIVE I2C.mWR
;

(read from EEPROM - old - separate W and R)
: read-old
  s0 loc
  s &i2c NATIVE I2C.mW
  
  6 r<n (bytes to read)
  r &i2c NATIVE I2C.mR
;

 
: Br (B=glasses=show :-)
  0 => i BEGIN
    r i + readb print cr
    i 1+ => i
    i buf# < AGAIN?
;


(both work!)
: test-old write read-old Br ;
: test-new write read Br ;


