# Byte code list
# --
	self.result=List
	self.metaInfo=Dict   # from pos as string to string
	
	self.addMeta=Lambda{
		P(1) as String => s
		pos=""+self.result.length
		self.metaInfo.get(pos,"")+s+" " => newMeta
		self.metaInfo.set(pos,newMeta)
	}
		
	
	
	self.addRawByte=Lambda{
		P(1) as int => byte
		error(byte<0 || byte > 255, "Invalid raw byte: " + byte)
		self.result.add(byte)
	}
	self.addRawBytes=Lambda{
		P(1) as List => list
		list->byte self.addRawByte(byte)
	}
	self.addRawWord=Lambda{
		P(1) as int => word
		word.bin(16).first(8).parseInt(2) => a
		word.bin(16).last(8).parseInt(2) => b
		self.addRawByte(a)
		self.addRawByte(b)
	}
	
	self.addChar=Lambda{
		P(1) as String => char
		error(char.length != 1, "Expected single character")
		code=char.toHexString("ISO-8859-1").parseInt(16)
		error(code < 33 || code > 127, "Invalid bytecode character: '" + char + "'")
		self.result.add(char)
	}
	self.patch=Lambda{
		P(1) as int => pos
		P(2) as List => data
		data->byte
			self.result.set(pos,byte)
			pos=pos+1
	}
		
	self.addString=Lambda{
		P(1) as String => s
		s.chars->c self.addChar(c)
	}
	self.length=Lambda{
		self.result.length
	}
	self.getValue=Lambda{
		P(1) as int => pos
		self.result.get(pos)
		
	}
	self.getMeta=Lambda{
		P(1) as int => pos
		if (self.metaInfo.has(""+pos)) self.metaInfo.get(""+pos) else ""
	}
	self.getCodeHex=Lambda{
		self.result->x
			num=x
			if (Sys.getType(num)=="String") {
				num=num.toHexString("ISO-8859-1").parseInt(16)
			}
			out(num.hex(2))
		| _.concat
		=>s
		if (s.length % 2 == 1) {
			error("Bytecode hex not even: " + s)
		}
		s
	}

/class Bytecode
