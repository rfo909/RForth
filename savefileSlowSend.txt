# Log file
# --
	Sys.savefile.dir.file("SlowSend.log")
/LogFile

# Newline
# --
	#"^n".unEsc.getBytes("ISO-8859-1").getList(0).nth(0)
	10
/Newline

# Send line
# --
	P(1) => line
	file=RawFile("/dev/ttyACM0")
	#println(line)
	ws=false
	line.getBytes("ISO-8859-1").getList(0)->byte
		file.write(byte)
		if (byte==32) {
			if (!ws) Sys.sleep(50)
			ws=true
		} else {
			ws=false
			Sys.sleep(10)
		}
	|
	file.write(Newline)
/SendLine


# Code directory
# --
	Sys.savefile.dir.sub("code")
/CodeDir


# Ask for file
# --
	Inner{CodeDir.files->f println(f.name)}
	Input("File name").get
//AskForFilename

# Send file
# --
	P(1) => fname
	
	if (fname==null) fname = AskForFilename
	file=CodeDir.file(fname)

	#terminate possible current loop waiting for key 
	SendLine(" ")
	
	file.read->line
		break(line.startsWith("#END"))
		reject(line.trim.startsWith("#"))
		if (line.trim.startsWith("@@")) {
			filename=line.trim.after("@@").trim
			SendFile(filename)
			continue
		} 	
		println(fname,"|",line)
		SendLine(" " + line)
	|
	println(fname,"COMPLETE")
/SendFile


# Main loop
# --
	Lib:Header("Enter line, q to stop, f to send file")
	loop
		readLine => str
		if (str=="f") {
			SendFile
			continue
		}
		break(str=="q")
		SendLine(str)
		LogFile.append(str)
		
/Main
