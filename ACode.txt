# Tags
# ####

#  :xxx = create tag
#  'xxx = insert tag address as LITERAL
#  *xxx = insert tag address as two bytes
#

# Defines 
# #######
#
#  _'xxx for literal (code) 
#  _*xxx for inline bytes
#

# ASCII strings
# #############
#
#   %t %r %u %e 0



def _'CELL 			LITERAL1 2		# defines cell size (Ref size) = 2
def _'REF_NULL		LITERAL1 0
def _*REF_NULL		0 0

def _*ASCII_SPACE	32
def _'ASCII_SPACE	LITERAL1 _*ASCII_SPACE

# Symbol Stack Entry (SSE)
# ########################

# 	[
#		value		Ref			2
#		next		Ref			2
#	]

# Basically a CONS cell (from Lisp)
# Allocated dynamically, linked via next ref

def _'SSE.value
def _'SSE.next				LITERAL1 2 ADD

def _'SSE.n?				LITERAL1	4


# Dictionary entry (DE)
# #####################

#	[ 
#		name				Ref			2
#		code				Ref			2
#		flags				byte		1
#		next 				Ref			2
#	]

# Allocated from the heap (next pointer)


def _'DE.name
def _'DE.code  LITERAL1 2 ADD
def _'DE.flags LITERAL1 4 ADD
def _'DE.next  LITERAL1 5 ADD


def _'DE.n?    LITERAL1 7 

# Status byte for DE's

def _'DE_Status_bit_immediate			LITERAL1 0x01

# Boolean constants

def _'FALSE		LITERAL1 0
def _'TRUE		LITERAL4  0xff 0xff 0xff 0xff




# Call Stack Frame (CSF)
# ######################

#	[ 
#		code				Ref			2
#		tempStackBase		byte		1
#		tempStackNext		byte		1
#		pc 					Ref			2
#	]

def _'CSF.code
def _'CSF.tempStackBase		LITERAL1 2 ADD
def _'CSF.tempStackNext		LITERAL1 3 ADD
def _'CSF.pc				LITERAL1 4 ADD

# Sizes

def _*CSF.n?				6
def _'CSF.n?				LITERAL1 _*CSF.n?		# Size of call stack frame, to navigate the call stack

def _*CS_maxDepth			30
def _'CS_maxDepth			LITERAL1 _*CS_maxDepth		# max call depth	

# Temp Stack Frame (TSF)
# ######################

#	[ 
#		name				Ref			2
#		value				Long		4
#	]

def _'TSF.name
def _'TSF.value				LITERAL1 2 ADD

# Sizes

def _*TSF.n?				6
def _'TSF.n?				LITERAL1 _*TSF.n?

def _*TS_maxDepth			100
def _'TS_maxDepth			LITERAL1 _*TS_maxDepth   # number of Temp Stack Frames (TSF)


# Some pseudo instructions
# ########################

def =$0		'SYM_0 SETLOCAL
def =$1		'SYM_1 SETLOCAL
def =$2		'SYM_2 SETLOCAL
def =$3		'SYM_3 SETLOCAL
def =$4		'SYM_4 SETLOCAL
def =$5		'SYM_5 SETLOCAL
def =$6		'SYM_6 SETLOCAL
def =$7		'SYM_7 SETLOCAL

def $0		'SYM_0 GETLOCAL
def $1		'SYM_1 GETLOCAL
def $2		'SYM_2 GETLOCAL
def $3		'SYM_3 GETLOCAL
def $4		'SYM_4 GETLOCAL
def $5		'SYM_5 GETLOCAL
def $6		'SYM_6 GETLOCAL
def $7		'SYM_7 GETLOCAL

def INCR					LITERAL1 1 ADD
def DECR					LITERAL1 1 SUBTRACT
def INCR2					LITERAL1 2 ADD

# -------------------------------------------------------------------------
# Light-weight call and return (used data stack to store return value)
# -------------------------------------------------------------------------

# Prefixing functions that use this mechanism by LW_

def _LW_CALL		=$a _CSF_CURR =$b $b _'CSF.pc READ2 =$c $a $b _'CSF.pc WRITE2
	# $a = target address
	# $b = CSF_CURR
	# $c = current pc
	
def _LW_RETURN =$a _CSF_CURR =$b $a $b _'CSF.pc WRITE2
	# $a = return address
	# $b = CSF_CURR
	
					
# local variables used in defines
 
def =$a		'SYM_a SETLOCAL
def =$b		'SYM_b SETLOCAL
def =$c		'SYM_c SETLOCAL
def =$d		'SYM_d SETLOCAL

def $a		'SYM_a GETLOCAL
def $b		'SYM_b GETLOCAL
def $c		'SYM_c GETLOCAL
def $d		'SYM_d GETLOCAL


# Op-codes for generating code

def OP:LITERAL1				99
def OP:LITERAL2				99
def OP:LITERAL4				99
def OP:CALL					99
def OP:RETURN				99
def OP:SETLOCAL				99
def OP:GETLOCAL				99

# SingleInstructionWords
# -----------------------

def OP:ADD					99
def OP:SUBTRACT				99
def OP:MUL					99
def OP:DIV					99
def OP:MOD					99
def OP:RIGHTSHIFT			99
def OP:LEFTSHIFT			99
def OP:AND					99
def OP:OR					99
def OP:NOT					99
def OP:EQ					99
def OP:NE					99
def OP:GT					99
def OP:LT					99
def OP:GE					99
def OP:LE					99



# Memory layout
#
# From this point we create tags and refer them, in order to do 
# function calls. The CALL operation is written in C, and manipulates
# the Call Stack (CS)
# ###############################################################################

0 				 # this position is the null-pointer address (not used)

:MAIN_SETUP			*MainSetup			# call this first
:MAIN_LOOP			*MainLoop			# then this, also after runtime PANIC

# Call stack

:CS_BASE_REF		*CS_DATA			# call stack base on heap
:CS_NEXT_FRAME		0					# index of next callstack frame (0-based)

# Symbol stack

:SYM_TOP_REF		*SYM_Cons_List 		# symbol table has initial hard coded content

# Temp stack

:TS_BASE_REF		*TEMP_STACK_DATA	# reserved data area
:TS_NEXT_FRAME		0					# single byte

# Dictionary pointer

:DICT_TOP			_*REF_NULL			# ref to first Dictionary Entry DE

# Heap management

:HERE               'END_MARKER			# two byte Ref to next free byte on heap
:HERE_SHADOW		'END_MARKER			# used by main loop, to rollback heap if panic





# Forth primitives
# --


:Malloc 					# n -- ptr # Sets all allocated bytes to zero
	=$0  # n
	'HERE READ2 =$1  			# ptr
	$1 $0 ADD 'HERE WRITE2 		# HERE = HERE + n
	
	# zero all bytes in allocated memory
	# --
	$1 =$2                      # copy pointer, for loop
	:xMalloc_clear_loop
		$2 'HERE READ2 EQ 'xMalloc_clear_end COND_JMP
		LITERAL1 0 $2 WRITE1
		$2 LITERAL1 ADD =$2
		'xMalloc_clear_loop JMP
	:xMalloc_clear_end
	$1							# return ptr
	RETURN
	
:Memcpy 					# ( targetRef srcRef count -- )
	=$1 # count
	=$2 # srcRef
	=$3 # targetRef
	
	:xMemcpy_loop
		$1 LITERAL1 0 LE 'xMemcpy_complete COND_JMP   # if count==0 goto complete
		$2 $1 ADD READ1  # source byte
			$3 $1 ADD WRITE1    # target write
		$1 LITERAL1 1 SUBTRACT =$1
		'xMemcpy_loop JMP
	:xMemcpy_complete
	RETURN
	


	
:HereCommit					# update HERE_SHADOW to HERE
	'HERE READ2 'HERE_SHADOW WRITE2
	RETURN
	

:CSF_current 				# returns pointer to current CSF (call stack frame)
	'CS_NEXT_FRAME READ1 
		LITERAL1 0 EQ 
			'xCSFc_underflow COND_JMP
	'CS_BASE_REF READ2 
		'CS_NEXT_FRAME READ1 _'CSF.n? MUL 
			ADD
	RETURN 
	
	:xCSFc_underflow
	'SYM_CSF_Underflow 'PANIC CALL
	
	
# ----------------------------------------------
# Statically allocated buffers
# ----------------------------------------------

def _CS_bytes				@{ mul _*CSF.n? _*CS_maxDepth }
def _TS_bytes				@{ mul _*TSF.n? _*TS_maxDepth }
def _CB_bytes				@{ mul 1 256 }

:CS_DATA					_CS_bytes			# call stack data

:TEMP_STACK_DATA			_TS_bytes			# temp stack data
		
:CompileBuf  				_CB_bytes			# Compile buffer (256 bytes)



:CompileBufPos				0					# Single byte
	
:OutByte					# (byte -- ) write to CompileBuf
	=$0								# byte
	'CompileBufPos READ1 =$1		# 
	$1 LITERAL1 254 GE 'xOutByte_fail COND_JMP
	
	$0 'CompileBuf $1 ADD WRITE1
	$1 INCR 'CompileBufPos WRITE1
	RETURN

	:xOutByte_fail
	'SYM_OutByte-Overflow 'PANIC CALL
	RETURN
	
:OutRef 					# ( Ref -- ) write to CompileBuf
	=$0
	OP:LITERAL2 'OutByte CALL
	$0 LITERAL1 8 RIGHTSHIFT 'OutByte CALL
	$0 'OutByte CALL
	RETURN	

:FirstNonSpace 				# move TIBs to first non-space character
	:xFNS_loop
		TIBs _'ASCII_SPACE NE COND_JMP 'xFNS_ok 
		TIBs+
		JMP 'xFNS_loop 
	:xFNS_ok
	RETURN
	
:FirstSpace 				# Move TIBr ahead to first space character
	TIBr<
	:xFS_loop
		TIBr _'ASCII_SPACE EQ 'xFS_ok COND_JMP
		TIBr+
		JMP 'xFS_loop
	:xFS_ok
	RETURN
	

:DE_create  				# name -- dictEntry # Create DE and push on dictionary stack

	=$0													# $0 = name
	_'DE.n? 'Malloc CALL =$1								# $1 = ptr to DE
	$0 $1 _'DE.name WRITE2								# DE.name=name
	'DICT_TOP READ2 $1 _'DE.next WRITE2					# DE.next=Dict_top
	$1 'DICT_TOP WRITE2									# Dict_top=DE
	$1													# return ptr
	RETURN


:CREATE						# Calls 'DE_create after obtaining 'NextWord 
	'NextWord CALL 'DE_create CALL
	DROP								# drop return value from 'DE_create
	RETURN

:NextWord 					# ( -- wordRef )
	'FirstNonSpace CALL					# set TIBs
	'FirstSpace CALL					# advance TIBr
	TIBW =$0							# create word from TIBs up to byte before TIBr
	TIBs>								# advance TIBs to TIBr
	$0									# return word
	RETURN

:COLON						# The colon compiler
	# Reset CompileBuf
	LITERAL1 0 'CompileBufPos WRITE1
	
	'NextWord CALL 'DE_create CALL =$1	# dictionary entry
	
	:xCOLON_LOOP
		'FirstNonSpace CALL
		'FirstSpace CALL
		TIBW =$2						# next word
		
		$2 'SYM_SEMICOLON EQ 'xCOLON_end COND_JMP		# if semicolon, end loop
		$2 'CompileWord CALL
	:xCOLON_end
		OP:RETURN 'OutByte CALL
		RETURN


:Immediate					# Set immediate flag on newest dictionary entry
	'DICT_TOP READ2 =$1					# ptr to topmost DE
	$1 LITERAL1 0 NE 'xImmediate_ok COND_JMP
		'SYM_Immediate_no_word 'PANIC CALL
	:xImmediate_ok
	
	$1 _'DE.flags READ1 =$2				# flags byte
	$2 _'DE_Status_bit_immediate OR =$2 # update flags value
	$2 $1 _'DE.flags WRITE1				# write updated flags to DE.flags
	RETURN	
	
	
def EMITRETURN =$a NE 'xSIW_@@ COND_JMP $a 'OutByte CALL _'TRUE RETURN :xSIW_@@
	# $d opcode
	# NE compares $a and 'SYM_XXX, jumps to end of line if not matching
	# if match, call OutByte with $d, then return true
		
:SingleInstructionWords					# ( word -- bool )
	=$0
	$0 'SYM_ADD 			OP:ADD 			EMITRETURN 
	$0 'SYM_SUBTRACT		OP:SUBTRACT		EMITRETURN 
	$0 'SYM_MUL				OP:MUL			EMITRETURN 
	$0 'SYM_DIV				OP:DIV			EMITRETURN 
	$0 'SYM_MOD				OP:MOD			EMITRETURN 
	$0 'SYM_RIGHTSHIFT		OP:RIGHTSHIFT	EMITRETURN 
	$0 'SYM_LEFTSHIFT		OP:LEFTSHIFT	EMITRETURN 
	
	$0 'SYM_AND				OP:AND			EMITRETURN 
	$0 'SYM_OR				OP:OR			EMITRETURN 
	$0 'SYM_NOT				OP:NOT			EMITRETURN 
	$0 'SYM_EQ				OP:EQ			EMITRETURN 
	$0 'SYM_NE				OP:NE			EMITRETURN 
	$0 'SYM_GT				OP:GT			EMITRETURN 
	$0 'SYM_LT				OP:LT			EMITRETURN 
	$0 'SYM_GE				OP:GE			EMITRETURN 
	$0 'SYM_LE				OP:LE			EMITRETURN 
	
	_'FALSE 
	RETURN

:CompileWord 					# ( word -- )
	=$0									# The word
	$0 'SingleInstructionWords NOT 'xCompileNotSIW COND_JMP
		RETURN
	:xCompileNotSIW
	
	$0 'DictLookup CALL =$1				# $1 = DE or NULL
	$1 _'REF_NULL EQ 'xCOMP_notInDict COND_JMP

		# Dictionary word found
			
		$1 _'DE.code =$2				# code pointer

		$1 _'DE.flags READ1 _'DE_Status_bit_immediate OR NOT 'xCOMP_regular COND_JMP

			# immediate word, call it right now

			$2 CALL
			RETURN

		:xCOMP_regular

			# Generate code for call
			
			$2 'OutRef CALL
			OP:CALL 'OutByte CALL
		
		:xCOMP_notInDict
			
			# Check if local variable set or get, returns true or false
				
			$0 'CompileLocalVariables 'xCOMP_done COND_JMP 

			# default to assume number
			
			$0 PARSE 					# will panic if not a number
				'CompileNumber CALL   
				
		:xCOMP_done
		   
		RETURN


:CompileLocalVariables			# ( word -- )
	=$0									# $0 = the word
	$0 READ1 %$ EQ 
		$0 INCR READ1 0 NE AND
			'xCLV_get COND_JMP
	$0 READ1 %= EQ
		$0 INCR READ1 %$ EQ AND
			$0 INCR2 READ1 0 NE AND
				'xCLV_set COND_JMP
	_'FALSE
	RETURN
	
	:xCLV_get
		# Generate code for getting local variable
		$0 INCR WORD 'OutRef CALL
		OP:GETLOCAL 'OutByte CALL
		_'TRUE
		RETURN

	:xCLV_set
		# Generate code for setting local varialbe
		$0 LITERAL1 2 ADD WORD 'OutRef CALL
		OP:SETLOCAL 'OutByte CALL
		_'TRUE
		RETURN
	
:CompileNumber 				# ( number -- )  
	=$0							# the number
	$0 LITERAL4 0xff 0xff 0x00 0x00 AND 'xCN_4 COND_JMP
	$0 LITERAL4 0x00 0x00 0xff 0x00 AND 'xCN_2 COND_JMP
		# 1 byte literal
		OP:LITERAL1 'OutByte CALL
		$0 'OutByte CALL
		RETURN
	:xCN_2
		# 2 byte literal
		OP:LITERAL2 'OutByte CALL
		$0 LITERAL1 8 RIGHTSHIFT 'OutByte CALL
		$0 'OutByte CALL
		RETURN
	:xCN_4
		# 4 byte literal
		OP:LITERAL4 'OutByte CALL
		$0 LITERAL1 24 RIGHTSHIFT 'OutByte CALL
		$0 LITERAL1 16 RIGHTSHIFT 'OutByte CALL	
		$0 LITERAL1 8 RIGHTSHIFT 'OutByte CALL
		$0 'OutByte CALL
		RETURN



:IsInt						# ( word -- bool )
	CHECK_PARSE
	RETURN
	

:ParseInt						# ( word -- number )		
	PARSE												# OP:PARSE may PANIC - check with IsInt first if in doubt
	RETURN
	

	
:DictLookup    				# ( wordRef -- ref ) returns _'REF_NULL or pointer to dict entry
	=$0  												# name we are looking for
	'DICT_TOP READ2 =$1 								# ptr = top dictionary entry (or null) 
	:xDL_loop
		$1 _'REF_NULL EQ 'xDL_fail COND_JMP				# if ptr == REF_NULL goto 'DL_fail
		$1 _'DE.name READ2 $0 EQ 'xDL_ok COND_JMP		# if $0.name == name GOTO 'DL_ok
		$1 _'DE.next READ2 =$1							# ptr = ptr.next
		'xDL_loop JMP									# repeat
	:xDL_fail
		_'REF_NULL
		RETURN
	:xDL_ok
		$1												# return ptr (to dictionary entry)
		RETURN
		



	
:PANIC        				# ( errorSymbol -- )
	=$0
	'SYM_PANIC		EMITWORD
	_'ASCII_SPACE 	EMIT
	$0 				EMITWORD
	
	PANIC						
		# reset input buffer
		# clear data- and call stack
		# then call :MainLoop
		
		
:DictAdd  					# ( name flags code -- )
	=$2										# code					    
	=$1										# flags
	=$0										# name
	$0 'DE_create CALL =$7					# ptr to DE
	$1 $7 _'DE.flags WRITE1					# DE.flags=flags
	$2 $7 _'DE.code WRITE2					# DE.code=code
	RETURN 	


def Immediate	_'DE_Status_bit_immediate
def Regular		0



:MainSetup   				# Call first time after boot or after hard Reset
	
	# Set HERE to byte following this memory map
	
	'END_MARKER 'HERE WRITE2
	'HereCommit CALL				# Update HERE_SHADOW to match HERE

	
	# Initial dictionary with words that can be called from Forth
	# mainly during compilation, hence immediate
	
	# name				Flags				Code			
	# -------------------------------------------------
	'SYM_COLON 			Immediate 			'COLON			'DictAdd CALL
	'SYM_IMMEDIATE		Immediate			'Immediate		'DictAdd CALL
	'SYM_HERE			Regular				'HERE			'DictAdd CALL
	'SYM_MALLOC			Regular				'Malloc			'DictAdd CALL
	'SYM_CREATE			Immediate			'CREATE			'DictAdd CALL
	'SYM_DictLookup		Regular				'DictLookup		'DictAdd CALL
	'SYM_OutByte		Regular				'OutByte		'DictAdd CALL
	'SYM_OutRef			Regular				'OutRef			'DictAdd CALL
	'SYM_Compile		Immediate			'CompileWord	'DictAdd CALL
	'SYM_NextWord		Regular				'NextWord		'DictAdd CALL
	'SYM_IsInt			Regular				'IsInt			'DictAdd CALL
	'SYM_ParseInt		Regular				'ParseInt		'DictAdd CALL
	'SYM_PANIC			Regular				'PANIC			'DictAdd CALL	

	# Commit the memory allocated for the above
	
	'HereCommit CALL
	
	RETURN


# Main loop is called after MainSetup, and again on PANIC, as it contains
# code to clean up after a panic (when HERE_SHADOW < HERE)

:MainLoop
	# If HERE_SHADOW != HERE, then we have arrived here
	# as a restart after a PANIC. 

	'HERE_SHADOW READ2 =$0										# $0 = HERE-shadow
	'HERE READ2 =$1												# $1 = HERE
	
	$1 $0 EQ 'xML_Here_ok COND_JMP								# if HERE == HERE-shadow, goto ok
		# remove entries from dictionary until DE ref 
		# smaller than HERE-shadow, which means they were
		# allocated by code that now has panicked
		
		'DICT_TOP READ2 =$2										# $2 = DE pointer
		:xML_DictPurge
			$2 $0 LT 'xML_DictPurgeOk COND_JMP					# includes NULL
			$2 _'DE.next READ2 =$2								# ptr=ptr.next
			'xML_DictPurge JMP
		:xML_DictPurgeOk
		$2 'DICT_TOP WRITE2										# update DICT_TOP

		# doing the same for symbol table
		
		'SYM_TOP_REF READ2 =$2									# $2 = Cons pointer
		:xML_SymPurge
			$0 $0 LT 'xML_SymPurgeOk COND_JMP
			$2 LITERAL1 2 ADD READ2 =$2							# read next pointer from cons 
			'xML_SymPurge JMP									
		:xML_SymPurgeOk
		
		'xML_SymPurgeOk
	
	:xML_Here_ok
	
	# The actual main loop
	
	:xML_Loop
		# Locate word, then compile it
		'NextWord CALL 
			'CompileWord CALL
		
		# Run code in CompileBuf
		'CompileBuf CALL
		
		# If there was a panic, we never reach this line, so getting here means
		# the word was ok. We commit the heap changes before repeating the loop
		
		'HereCommit CALL
		
		'xML_Loop JMP

		
		
:SYM_Cons_List		# hard coded list of symbols 
					# New symbols will be added from C, mostly from TIBW or WORD ops

	*SYM_0					NEXT	
	*SYM_1					NEXT	
	*SYM_2					NEXT	
	*SYM_3					NEXT	
	*SYM_4					NEXT	
	*SYM_5					NEXT	
	*SYM_6					NEXT	
	*SYM_7					NEXT	
	*SYM_a					NEXT	
	*SYM_b					NEXT	
	*SYM_c					NEXT	
	*SYM_d					NEXT	
	
	*SYM_ADD				NEXT
	*SYM_SUBTRACT			NEXT
	*SYM_MUL				NEXT
	*SYM_DIV				NEXT
	*SYM_MOD				NEXT
	*SYM_RIGHTSHIFT			NEXT
	*SYM_LEFTSHIFT			NEXT
	*SYM_AND				NEXT
	*SYM_OR					NEXT
	*SYM_NOT				NEXT
	*SYM_EQ					NEXT
	*SYM_NE					NEXT
	*SYM_GT					NEXT
	*SYM_LT					NEXT
	*SYM_GE					NEXT
	*SYM_LE					NEXT
	*SYM_COLON				NEXT
	*SYM_SEMICOLON			NEXT
	*SYM_IMMEDIATE			NEXT
	*SYM_HERE				NEXT
	*SYM_MALLOC				NEXT
	*SYM_CREATE				NEXT
	*SYM_DictLookup			NEXT
	*SYM_OutByte			NEXT
	*SYM_OutRef				NEXT
	*SYM_Compile			NEXT
	*SYM_NextWord			NEXT
	*SYM_PANIC				NEXT
	*SYM_ParseInt			NEXT
	*SYM_IsInt				NEXT
	*SYM_CSF_Underflow		NEXT
	*SYM_Immediate_no_word	NEXT
	*SYM_Unknown-Word		NEXT
	*SYM_OutByte-Overflow	NO_NEXT
	
	
	
	



	
:SYM_0 %0 0
:SYM_1 %1 0
:SYM_2 %2 0
:SYM_3 %3 0
:SYM_4 %4 0
:SYM_5 %5 0
:SYM_6 %6 0
:SYM_7 %7 0
:SYM_a %a 0
:SYM_b %b 0
:SYM_c %c 0
:SYM_d %d 0

:SYM_ADD %+ 0
:SYM_SUBTRACT %- 0
:SYM_MUL %* 0
:SYM_DIV %/ 0
:SYM_MOD %% 0
:SYM_RIGHTSHIFT %> %> 0
:SYM_LEFTSHIFT %< %< 0
:SYM_AND %a %n %d 0
:SYM_OR %o %r 0
:SYM_NOT %n %o %t 0
:SYM_EQ %e %q 0
:SYM_NE %n %e 0
:SYM_GT %g %t 0
:SYM_LT %l %t 0
:SYM_GE %g %e 0
:SYM_LE %l %e 0
:SYM_COLON %: 0
:SYM_SEMICOLON %; 0
:SYM_IMMEDIATE %I %M %M %E %D %I %A %T %E 0
:SYM_HERE %H %E %R %E 0
:SYM_MALLOC %M %A %L %L %O %C 0
:SYM_CREATE %C %R %E %A %T %E 0
:SYM_DictLookup %? %? 0
:SYM_OutByte %O %u %t %B %y %t %e 0
:SYM_OutRef %O %u %t %R %e %f 0
:SYM_Compile %C %o %m %p %i %l %e 0
:SYM_NextWord %N %e %x %t %W %o %r %d 0
:SYM_PANIC %P %A %N %I %C 0
:SYM_IsInt %I %s %I %n %t 0
:SYM_ParseInt %P %a %r %s %e %I %n %t 0
:SYM_CSF_Underflow %C %a %l %l %- %s %t %a %c %k %- %u %n %d %e %r %f %l %o %w 0
:SYM_Immediate_no_word %I %m %m %e %d %i %a %t %e %- %n %o %- %w %o %r %d 0
# --
:SYM_Unknown-Word %U %n %k %n %o %w %n %- %W %o %r %d 0
:SYM_OutByte-Overflow %O %u %t %B %y %t %e %- %O %v %e %r %f %l %o %w 0


# This is the initial value of HERE

:END_MARKER






