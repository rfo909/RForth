

# Using &CompileBuf 

&CompileBuf CONSTANT Buf (64 bytes)
32 CONSTANT PageSize
0x50 CONSTANT i2cAddr

: Reset (--) Buf BufReset ;
: Add (byte --) Buf BufAdd ;
: Hi (word -- byte) 8 >> 0xFF & ;
: Lo (word -- byte) 0xFF & ;
: AddWord (word -- ) dup Hi Add Lo Add ;


(
Populate page with EEPROM page index as 2 bytes
and up to PageSize bytes of data from the heap
-----------------------------------------------
)
: PopulatePage (pageIndex --)
  => pIndex
  
  Reset
  pIndex 1+ AddWord (write address)
  
  (iterate page pos 0-31)
  0 => ppos
  0 => mempos  (read pos used inside loop)
  
  BEGIN
    &PROTECT 
      pIndex PageSize * + 
      ppos + => mempos
      
    mempos HERE < IF
       mempos readb Add
    THEN
    ppos 1+ => ppos
    ppos PageSize < AGAIN?
;      

(
Save page prepared by PopulatePage to EEPROM
--------------------------------------------
)
: SavePage (--)
  (buf contains two address bytes + data)
  Buf 1+ @ => addr
  "SavePage_ .str addr print# cr
  Buf i2cAddr NATIVE I2C.masterWrite
  i2cAddr NATIVE I2C.masterWWait
;



(
The page at EEPROM address 0x00 contains
meta-information, contains length and the
&PROTECT value; if the protect value has changed,
we can not restore a save
-------------------------------------------
)
: StatusPage (--)
  HERE &PROTECT - => length
  
  Reset
  (eeprom address 0)
  0 AddWord
 
  (length value)
  length AddWord

  &PROTECT AddWord
    
  SavePage
;


(
Save heap between &PROTECT and HERE. This includes the
memory location where HERE is defined, and other
variable data from ACode. It does not include the stacks,
except the compile stack, because those other stacks
are maintained in C, outside the heap
---------------------------------------------------------
)  
: SaveHeap (--)
  StatusPage
  0 => page
  0 => done
  BEGIN
    page PopulatePage
    (has content beyond EE pos bytes?)
    Buf readb 2 > IF
      SavePage
      page 1+ => page
    ELSE
      1 => done
    THEN
    done not AGAIN?
    cr "Complete .str
;



