# Log file
# --
	Sys.savefile.dir.file("SlowSend.log")
/LogFile

# Newline
# --
	#"^n".unEsc.getBytes("ISO-8859-1").getList(0).nth(0)
	10
/Newline

# Serial device
# --
	RawFile("/dev/ttyACM0")
/Device

# Send line
# --
	P(1) as RawFile => device
	P(2) as String => line
	
	#println(line)
	ws=false
	line.getBytes("ISO-8859-1").getList(0)->byte
		device.write(byte)
		if (byte==32) {
			if (!ws) Sys.sleep(50)
			ws=true
		} else {
			ws=false
			Sys.sleep(10)
		}
	|
	device.write(Newline)
/SendLine


# Code directory
# --
	Sys.savefile.dir.sub("code")
/CodeDir


# Ask for file
# --
	Inner{CodeDir.files->f println(f.name)}
	Input("File name").get
//AskForFilename

# Send file
# --
	P(1) as RawFile => device
	P(2) as String? => fname
		
	if (fname==null) fname = AskForFilename
	file=CodeDir.file(fname)

	#terminate possible current loop waiting for key 
	SendLine(" ")
	
	file.read->line
		break(line.startsWith("#END"))
		reject(line.trim.startsWith("#"))
		if (line.trim.startsWith("@@")) {
			filename=line.trim.after("@@").trim
			SendFile(device,filename)
			continue
		} 	
		println(fname,"|",line)
		SendLine(device," " + line)
	|
	println(fname,"COMPLETE")
/SendFile


# Main loop
# --
	device=Device
	
	Lib:Header("Enter line, q to stop, f to send file")
	loop
		readLine => str
		if (str=="f") {
			SendFile(device)
			continue
		}
		break(str=="q")
		SendLine(device,str)
		LogFile.append(str)
	|
	device.close
		
/Main
